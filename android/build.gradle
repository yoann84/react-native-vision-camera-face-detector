def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}
def kotlinVersion = safeExtGet("VisionCameraFaceDetector_kotlinVersion", "2.1.20")

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.13.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

android {
    buildToolsVersion = safeExtGet("VisionCameraFaceDetector_buildToolsVersion", "35.0.0")
    ndkVersion safeExtGet("VisionCameraFaceDetector_ndkVersion", "27.3.13750724")
    defaultConfig {
        minSdkVersion safeExtGet("VisionCameraFaceDetector_minSdkVersion", 26)
        compileSdkVersion safeExtGet("VisionCameraFaceDetector_compileSdkVersion", 35)
        targetSdkVersion safeExtGet("VisionCameraFaceDetector_targetSdkVersion", 35)
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable "GradleCompatible"
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
}

dependencies {
    implementation "com.facebook.react:react-native:+"  // From node_modules
    
    // Vision Camera - optional Gradle dependency
    // When using GitHub URLs, this project might not be available during Gradle evaluation
    // but React Native autolinking provides it at runtime
    def visionCameraProject = findProject(":react-native-vision-camera")
    if (visionCameraProject != null) {
        compileOnly project(":react-native-vision-camera")
    }
    // If not found, classes are still available at runtime via autolinking
    
    implementation "androidx.annotation:annotation:1.9.1"
    implementation "androidx.camera:camera-core:1.5.1"
    implementation "com.google.mlkit:face-detection:16.1.7"
}
